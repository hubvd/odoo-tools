#!/bin/fish

set root (path normalize (status dirname)/../lint)
set rundir $PWD

function _relativize
  set rel (realpath --relative-to $root $rundir)
  for path in $argv
    switch $path
      case '-*' '/*'
        echo $path
      case '*'
        path normalize $rel/$path
    end
  end
end

function _run
  set exe $root/node_modules/.bin/$argv[1]
  test -f $exe || exit 1
  cd $root
  set options (string match -- '-*' $argv[2..])
  set args (string match -v -- '-*' $argv[2..])
  $exe $options (_relativize $args)
end

function eslint
  argparse fix -- $argv
  set js_files (string match -r '.*(?:\.d\.ts|\.js)$' $argv)
  and set -q js_files[1]
  and _run eslint $_flag_fix --resolve-plugins-relative-to=$root $js_files
end

function lint
  eslint $argv
end

function fix
  eslint --fix $argv
end

if string match -qv '_*' "$argv[1]" && functions -q "$argv[1]"
  $argv
else
  lint $argv
end
