#!/usr/bin/fish

argparse 'v/version' 'p/path' 'l/list' 'r/repo' -- $argv

set root ~/src

function validate_workspace
  test -f $argv[1]/odoo/odoo-bin
end

function sort_workspaces
  string join \n $argv | string replace -r '(saas-)?([\d.]+)' '$2:$1$2' | string replace master 999:master | sort | string replace -r '.*:(.*)' '$1'
end

if set -q _flag_l
  for i in (sort_workspaces (find $root -mindepth 1 -maxdepth 1 -type d | string replace -a $root/ ''))
    if validate_workspace $root/$i
      set -q _flag_version && echo $i || echo $root/$i
    end
  end
  exit 0
end

test (count $argv) -eq 1 && set wd (realpath $argv[1]) || set wd (realpath $PWD)

# If not inside a directory inside root -> fail
string match -rq "^$root/.*" $wd || exit 1

set path_from_root (string replace $root/ '' $wd)
set parts (string split '/' $path_from_root)
set ws_version $parts[1]
validate_workspace $root/$ws_version || exit 1

if set -q _flag_v
  # ie: 14.0
  echo $ws_version
else if set -q _flag_p
  # ie: /home/odoo/src/14.0
  echo $root/$ws_version
else if set -q _flag_r
  set repo $parts[2]
  if contains "$repo" odoo enterprise design-themes
    echo $repo
  else
    exit 1
  end
end
