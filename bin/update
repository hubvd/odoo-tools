#!/bin/fish

argparse 'f/force' -- $argv

set data (worktree current fish) && eval $data
set base $workspace_base

if not test $workspace_name = $workspace_base
    exit 1
end

set repo_paths (path filter -d $workspace_path/{odoo,enterprise,design-themes})

if not set -q _flag_force
  for repo_path in $repo_paths
    set branch (git -C $repo_path branch --show-current)
    if test "$branch" != $base
      echo (set_color yellow)"skipped $base because $(path basename $repo_path) is @ $branch"(set_color normal)
      exit 1
    end
  end
end


for repo_path in $repo_paths
  if test (git -C $repo_path rev-list --count --left-only @{upstream}...@) = 0
    continue
  end

  set staged_files (
    git -C $repo_path status --porcelain |
    string match -r '^[AMRDC].*' |
    string sub -s4 |
    string unescape --style script
  )

  if test (count $staged_files) -gt 0
    echo skipped $repo because there are changes
    continue
  end

  git -C $repo_path reset --hard origin/$base
end

