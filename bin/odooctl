#!/bin/fish

function _pids
  ps a -o pid,args | string match -rg '^\s+(?P<pid>\d+).*python (?:odoo/odoo-bin|\S+/patches/main.py|.*/odoo-tools/launcher/main.py) (?!shell)'
end

function _get_port_from_pid
  ss -H -tlnp | string match -rg ':(\d+).*'$argv[1]
end

function _get_database
  cat /proc/$argv[1]/cmdline | tr '\0' \n | string match -rg  -- '--database=(.*)'
end

function killall
  set pids (_pids) && kill $pids
end

function instances
  for pid in (_pids)
    set port (_get_port_from_pid $pid)
    set db (_get_database $pid)
    string join : $pid $port $db
  end
end

if not set -q argv[1]
  instances
else
  string match -qv '_*' "$argv[1]" && functions -q "$argv[1]" && $argv
end
