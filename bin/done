#!/usr/bin/fish

argparse 'amend' -- $argv

function modules --inherit _flag_amend
  if set -q _flag_amend
    set files (git diff-tree --no-commit-id --name-only -r @)
  end
  set -a files (git status --porcelain | rg '^[AMRDC]+\s+(?P<path>.*)' -or'$1')
  for file in $files
    set parts (string split / $file)
    switch (worktree repository)
      case 'odoo'
        if test "$parts[1]" = "addons" && set -q parts[2] && not contains $parts[2] $modules
          set -a modules $parts[2]
        else if test "$parts[1]" = "odoo" && test "$parts[2]" = "addons" && set -q parts[3] && not contains $parts[3] $modules
          set -a modules $parts[3]
        end
      case 'enterprise'
        if set -q parts[1] && not contains $parts[1] $modules
          set -a modules $parts[1]
        end
      case '*'
        exit 1
    end
  end

  if set -q modules
    string join ', ' $modules
  else
    echo
  end
end

function parse_ticket_id
  string match -rq 'opw-(?P<id>\d+)' $argv[1] && echo $id || echo TODO
end

function opw
  parse_ticket_id (git rev-parse --abbrev-ref HEAD)
end

function msg
  set parts '[FIX] '(modules):
  set -a parts "Steps to reproduce"\n"=================="
  set -a parts "Cause of the issue"\n"=================="
  set -a parts Solution\n========
  set -a parts opw-(opw)
  string join \n\n $parts
end

if set -q _flag_amend
  git commit --amend -eF (msg | psub)
else
  git commit -eF (msg | psub)
end
