#!/usr/bin/fish

set pycharm_dir ~/.local/share/JetBrains/Toolbox/apps/PyCharm-P/ch-0
set pycharm_latest_version (
   path dirname $pycharm_dir/*/bin/pycharm.sh |
   path dirname |
   path basename |
   sort -t . -k1,1n -k2,2n -k3,3n |
   tail -1
)
set pycharm_bin $pycharm_dir/$pycharm_latest_version/bin/pycharm.sh
test -f "$pycharm_bin" || exit 1

function open
    setsid $pycharm_bin $argv &>/dev/null &
end

if set -q argv[1]
    set parts (string split '#' -- (string replace -r '\.pycharm$' '' -- $argv[1]))
    if set -q parts[2]
        open --line $parts[2] $parts[1]
    else
        open $parts[1]
    end
    exit $status
end

set choice (worktree path) || begin
    set names (worktree names)
    set choice (string join \n $names | bemenu -l (count $names) -p pycharm) || exit 1
    set index (contains -i $choice $names) || exit 1
    set paths (worktree paths)
    set choice $paths[$index]
end

if test -d "$choice"
    open $choice
else
    exit 1
end
