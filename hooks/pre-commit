#!/bin/fish

function fail
  echo $argv 2>&1
  exit 1
end

set base (worktree base)

function check_branch
  if test "$base" = "$(git rev-parse --abbrev-ref HEAD)"
    fail "Can't commit to a base branch"
  end
end

function check_sticky
  if not contains $base 1{5,6,7}.0 saas-{16.{3,4},17.1} master
    fail "Can't commit to a non sticky base branch"
  end
end

set staged_files (
  git status --porcelain |
  string match -r '^[AMRDC].*' |
  string sub -s4 |
  string unescape --style script
)

function fail_with_pattern -a glob -a pattern -a flags
  set files (string match $glob -- $staged_files) || return
  set matches (rg -l --no-config $flags "$pattern" $files)
  and fail "Found `$pattern` in $matches"
end

function check_debugger
  fail_with_pattern "*.js" "debugger"
end

function check_qunit
  fail_with_pattern "*/static/tests/*.js" "QUnit\s*\.\s*(debug|only)" -U
end

for check in (functions --names | string match 'check_*')
  $check
end

true

